plugins {
    id 'java-library'
    id 'scala'
    id "com.github.maiflai.scalatest" version "0.32"
    id 'maven-publish'
    id 'signing'
}

project.ext.scalaMajorVersion = "2.13"
project.ext.scala2javaVersion = "6.3.0"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withJavadocJar()
    withSourcesJar()
}

group 'io.github.effiban'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.github.effiban:scala2java-spi_$scalaMajorVersion:$scala2javaVersion"
}

testing {
    suites {
        configureEach {
            dependencies {
                implementation "io.github.effiban:scala2java-test-utils_$scalaMajorVersion:$scala2javaVersion"
                runtimeOnly 'com.vladsch.flexmark:flexmark-all:0.64.0'
            }
        }
        test {
            targets {
                all {
                    testTask.configure {
                        maxParallelForks = 1
                    }
                }
            }
        }
        integrationTest(JvmTestSuite) {
            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

publishing {
    publications {
        scala2javaextScalatest(MavenPublication) {
            artifactId = "${project.name}_$scalaMajorVersion"
            from components.java

            pom {
                name = 'Scala2Java Mockito Extension'
                description = 'Scala2Java extension for translating ScalaTest to JUnit / Hamcrest'
                url = 'https://github.com/effiban/scala2javaext-scalatest'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'effiban'
                        name = 'Effi Ban'
                        email = 'effi.ban@skai.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/effiban/scala2javaext-scalatest.git'
                    developerConnection = 'scm:git:ssh://github.com/effiban/scala2javaext-scalatest.git'
                    url = 'https://github.com/effiban/scala2javaext-scalatest'
                }
            }
        }
    }
    repositories {

        def user = project.hasProperty('ossrhUsername') ? "$ossrhUsername" : ''
        def pass = project.hasProperty('ossrhPassword') ? "$ossrhPassword" : ''

        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = user
                password = pass
            }
        }
    }
}

signing {
    sign publishing.publications.scala2javaextScalatest
}
